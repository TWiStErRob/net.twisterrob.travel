if (project.hasProperty('libFromSources')) {
	evaluationDependsOn ':twister-lib-android'
}
//name = 'Better London Travel Android App'
apply plugin: 'com.android.application'
apply plugin: 'net.twisterrob.android-app'

dependencies {
	if (project.hasProperty('libFromSources')) {
		compile project(':twister-lib-android')
	} else {
		compile('net.twisterrob:twister-lib-android:1.0-SNAPSHOT')
	}
	compile(project(':Shared')) {
		exclude module: 'android-polyfill'
	}
	// TOFIX otherwise VariantFragment descendants don't see Fragment and its methods (Cannot access FQCN errors in IDEA)
	compile 'com.google.android.gms:play-services-maps:9.2.1'
	compile 'com.google.android.gms:play-services-places:9.2.1'
	compile "com.android.support:support-v4:${VERSION_SUPPORT}"
	compile "com.android.support:appcompat-v7:${VERSION_SUPPORT}"
	compile "com.android.support:design:${VERSION_SUPPORT}"
//	compile "com.android.support:cardview-v7:${VERSION_SUPPORT}"
//	compile "com.android.support:recyclerview-v7:${VERSION_SUPPORT}"
//	compile 'com.facebook.stetho:stetho:1.3.1'

	apply from: "${rootDir}/gradle/testCompile.gradle", to: project
}

android {
	defaultConfig {
		applicationId 'net.twisterrob.blt'
		version {
			versionFile = null
			major = 1
		}
	}
	buildTypes {
		release {
			minifyEnabled true
			shrinkResources true
		}
	}
	productFlavors {
		full {
			// default
		}
		range {
			applicationId 'net.twisterrob.blt.range'
		}
	}

	sourceSets {
		// STOPSHIP figure out another way
		main.java.srcDir 'src/main/hack'
	}
}

task generateDataBase(type: Copy, dependsOn: [':Data:runNetwork', ':Data:runPostCode']) {
	from(new File(project(':Data').projectDir, 'output')) {
		include '*.data.*.sql'
	}
	into android.sourceSets.main.assets.srcDirs[0]
}
android.applicationVariants.all { /*com.android.build.gradle.api.ApplicationVariant*/ variant ->
	variant.mergeAssets.dependsOn tasks.generateDataBase
}
