//name = "Better London Travel Android App"
plugins {
	id("net.twisterrob.blt.convention")
	id("net.twisterrob.gradle.plugin.android-app")
	id("com.android.application")
}

dependencies {
	implementation("net.twisterrob.lib:twister-lib-android-monolith")
	implementation("net.twisterrob.lib:twister-lib-android-basics")
	implementation("net.twisterrob.lib:twister-lib-android-slf4j")
	implementation("net.twisterrob.lib:twister-lib-android-widgets")
	implementation("net.twisterrob.lib:twister-lib-android-stringers")

	implementation("net.twisterrob.lib:twister-lib-android-about")
	implementation("net.twisterrob.lib:twister-lib-android-color_picker")
	implementation("net.twisterrob.lib:twister-lib-android-settings")

	implementation(projects.shared) {
		exclude module: "android-polyfill"
	}
	implementation(libs.gms.maps)
	implementation(libs.gms.places)
	implementation(libs.jsr305)
	implementation(libs.glide)

	testImplementation(projects.shared.testHelpers)
}

android {
	namespace = "net.twisterrob.blt.android"
	compileSdk = 34
	defaultConfig {
		minSdk = 21
		targetSdk = 21
		version {
			major = 1
		}
	}
	buildTypes {
		release {
			minifyEnabled true
			shrinkResources true
		}
		debug {
			// twister-lib-android only has release build type
			matchingFallbacks = [ "release" ]
		}
	}
	buildFeatures {
		buildConfig = true
	}
	lint {
		checkAllWarnings = true
		checkDependencies = true
		abortOnError = false
		lintConfig = rootProject.file("config/lint/lint.xml")
		baseline = rootProject.file("config/lint/lint-baseline.xml")
	}
	flavorDimensions += "app-build"
	productFlavors {
		full {
			// default
			applicationId = "net.twisterrob.blt"
			dimension("app-build")
		}
		range {
			applicationId = "net.twisterrob.blt.range"
			dimension("app-build")
		}
	}
}

abstract class GenerateDatabase extends Copy {

	@Internal("An intermediate property to satisfy AGP, tracked in destination.")
	abstract DirectoryProperty getOutput()
}

def generateDataBase = tasks.register("generateDataBase", GenerateDatabase.class) {
	dependsOn(":Data:runNetwork", ":Data:runPostCode")
	from(new File(projects.data.dependencyProject.projectDir, "output")) {
		include("*.data.*.sql")
	}
	into(output)
}
androidComponents {
	onVariants(selector().all()) { variant ->
		variant.sources.assets.addGeneratedSourceDirectory(generateDataBase, { it.output })
		afterEvaluate {
			tasks.named("generate${variant.name.capitalize()}Assets").configure {
				// This should be not necessary with addGeneratedSourceDirectory, but without this, it doesn't work.
				dependsOn(generateDataBase)
			}
		}
	}
}
