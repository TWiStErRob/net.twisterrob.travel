plugins {
	id("net.twisterrob.blt.convention")
	id("org.gradle.application")
	id("net.twisterrob.gradle.plugin.java")
}

dependencies {
	implementation("net.twisterrob.lib:twister-lib-general")
	implementation(projects.common.data.desktop)
}

application {
	mainClass = "net.twisterrob.blt.data.apps.PostCodeAreas"
}

tasks.named("run", JavaExec.class) {
	File inputDir = rootProject.file("temp/feed15/LULDLRRiverTramCable")
	File csv = projects.common.data.fake.dependencyProject.file("src/main/data/london.csv.gz")
	File outputDir = file("output")
	// STOPSHIP File outputFile = new File(outputDir, "LondonTravel.data.AreaHull.sql")
	File logFile = new File(outputDir, "run.log")
	inputs.dir(inputDir).withPathSensitivity(PathSensitivity.RELATIVE)
	inputs.file(csv).withPathSensitivity(PathSensitivity.NONE)
	outputs.dir(outputDir)
	args = [ csv ]
	enableAssertions = true
	doFirst {
		outputDir.mkdirs()
		standardOutput = logFile.newOutputStream()
	}
	doLast {
		standardOutput.close()
	}
}

configurations.create("output")
artifacts.output(tasks.named("run").map { it.outputs.files.singleFile })
