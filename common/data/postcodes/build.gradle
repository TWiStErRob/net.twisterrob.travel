import net.twisterrob.travel.JavaExecExtensions

plugins {
	id("net.twisterrob.blt.convention")
	id("org.gradle.application")
	id("net.twisterrob.gradle.plugin.java")
}

dependencies {
	implementation("net.twisterrob.lib:twister-lib-general")
	implementation(projects.common.data.desktop)
}

application {
	mainClass = "net.twisterrob.blt.data.apps.PostCodeAreas"
}

tasks.named("run", JavaExec.class) {
	File inputDir = rootProject.file("temp/feed15/LULDLRRiverTramCable")
	RegularFile csv = rootProject.layout.projectDirectory.file("data/london.csv.gz")
	Provider<Directory> outputDir = project.layout.buildDirectory.dir("output")
	// STOPSHIP File outputFile = new File(outputDir, "LondonTravel.data.AreaHull.sql")
	inputs.dir(inputDir).withPathSensitivity(PathSensitivity.RELATIVE)
	inputs.file(csv).withPathSensitivity(PathSensitivity.NONE)
	args = [ csv.asFile ]
	enableAssertions = true
	JavaExecExtensions.logTo(it, outputDir.map { it.file("run.log") }) 
}

configurations.create("output")
artifacts.output(tasks.named("run").map { it.outputs.files.singleFile })
