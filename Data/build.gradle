// name = 'twister-travel-data'
version = '0.0.1-SNAPSHOT'
// <name>Better London Travel Data Generation</name>
// <description>Standalone project to handle large feeds</description>

apply plugin: 'net.twisterrob.java'

dependencies {
	//configurations.compile.resolutionStrategy.cacheChangingModulesFor 0, 'seconds' // -SNAPSHOT
	compile project(':Shared')
	compile 'org.slf4j:slf4j-simple:1.7.5'
	compile 'org.xerial:sqlite-jdbc:3.7.15-M1'
}

apply plugin: 'jetty'
// TODO logging doesn't work with org.eclipse nor org.mortbay package names
// -Dorg.mortbay.log.class=org.eclipse.jetty.util.log.StrErrLog changes the logger, but no output
// --debug logs, but too much and not configurable
JettyRun jettyRun = tasks[JettyPlugin.JETTY_RUN] as JettyRun
jettyRun.classpath = files([ ]) // to prevent running :Data:classes task
jettyRun.daemon = true
jettyRun.reload = JettyPlugin.RELOAD_MANUAL
jettyRun.webAppSourceDirectory = file('src/data')
jettyRun.httpPort = 8081
//jettyRun.additionalRuntimeJars = configurations.compile.findAll { it.name.contains('slf4j-simple') }

apply plugin: 'idea'
idea {
	module {
		excludeDirs = [ file('output'), file('src/data') ]
	}
}

apply plugin: 'application'

task runNetwork(type: JavaExec) {
	group = ApplicationPlugin.APPLICATION_GROUP
	classpath = sourceSets.main.runtimeClasspath
	main = 'net.twisterrob.blt.data.apps.TravelNetworkParser'
	enableAssertions = true
}

task runPostCode(type: JavaExec) {
	group = ApplicationPlugin.APPLICATION_GROUP
	classpath = sourceSets.main.runtimeClasspath
	main = 'net.twisterrob.blt.data.apps.PostCodeAreas'
	enableAssertions = true
}

task runDisplay(type: JavaExec) {
	group = ApplicationPlugin.APPLICATION_GROUP
	classpath = sourceSets.main.runtimeClasspath
	main = 'net.twisterrob.blt.data.apps.DisplayLine'
	args = [ 'DLR' ]
	if (project.hasProperty('lines')) {
		args = project.property('lines').toString().split(',') as List;
	}
	enableAssertions = true
}
