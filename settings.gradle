import static net.twisterrob.gradle.settings.SettingsUtils.enableFeaturePreviewQuietly

pluginManagement {
	includeBuild("gradle/plugins")
	repositories {
		google()
		mavenCentral()
		gradlePluginPortal()
	}
}

plugins {
	id("net.twisterrob.gradle.plugin.settings") version "0.16"
}

rootProject.name = "net-twisterrob-blt"

include(":android:app:full")
include(":android:app:range")
include(":android:feature:range")
include(":desktop:routes")
include(":web:status-history")
include(":data:network")
include(":data:routes")
include(":data:postcodes")
include(":data:feed:timetable")
include(":data:feed:trackernet")
include(":data:feed:facilities")
include(":shared:android-polyfill")
include(":shared:model")
include(":shared:diff")
include(":shared:test-helpers")

enableFeaturePreviewQuietly(settings, "TYPESAFE_PROJECT_ACCESSORS", "Type-safe project accessors")

dependencyResolutionManagement {
	repositoriesMode = RepositoriesMode.FAIL_ON_PROJECT_REPOS
	repositories {
		google()
		mavenCentral()
		maven {
			name = "jitpack.io"
			url = uri("https://jitpack.io/")
			content {
				includeModule("com.github.martin-stone", "hsv-alpha-color-picker-android")
			}
		}
	}
}

includeBuild("libs") { build ->
	apply from: new File(build.projectDir, "gradle/settings.substitutions.gradle"), to: build
}
