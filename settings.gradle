import static net.twisterrob.gradle.GradleUtils.doNotNagAbout
import static net.twisterrob.gradle.settings.SettingsUtils.enableFeaturePreviewQuietly

pluginManagement {
	includeBuild("gradle/plugins")
	repositories {
		google()
		mavenCentral()
		gradlePluginPortal()
	}
}

plugins {
	id("net.twisterrob.gradle.plugin.settings") version "0.18"
	id("net.twisterrob.gradle.plugin.nagging") version "0.18"
	id("com.gradle.develocity") version "4.0.2"
	id("org.gradle.toolchains.foojay-resolver-convention") version "0.10.0"
}

rootProject.name = "net-twisterrob-travel"

include(":android:app:full")
include(":android:app:range")
include(":android:component:android")
include(":android:component:map")
include(":android:component:data")
include(":android:component:theme")
include(":android:feature:range")
include(":common:android-polyfill")
include(":common:data:desktop")
include(":common:data:fake")
include(":common:data:network")
include(":common:data:postcodes")
include(":common:data:static")
include(":common:diff")
include(":common:feed:core")
include(":common:feed:feeds")
include(":common:feed:facilities")
include(":common:feed:timetable")
include(":common:feed:trackernet")
include(":common:log-console")
include(":common:model")
include(":common:maptiler")
include(":common:test-helpers")
include(":domain:london")
include(":domain:status")
include(":desktop:routes")
include(":web:status-history")

enableFeaturePreviewQuietly(settings, "TYPESAFE_PROJECT_ACCESSORS", "Type-safe project accessors")

dependencyResolutionManagement {
	repositoriesMode = RepositoriesMode.FAIL_ON_PROJECT_REPOS
	repositories {
		google()
		mavenCentral()
		maven {
			name = "jitpack.io"
			url = uri("https://jitpack.io/")
			content {
				includeModule("com.github.martin-stone", "hsv-alpha-color-picker-android")
			}
		}
	}
}

includeBuild("libs") { build ->
	apply from: new File(build.projectDir, "gradle/settings.substitutions.gradle"), to: build
}

develocity {
	buildScan {
		termsOfUseUrl = "https://gradle.com/help/legal-terms-of-use"
		termsOfUseAgree = "yes"
	}
}

String gradleVersion = GradleVersion.current().version

// TODEL Gradle 8.13 vs AGP 8.0-8.9 https://issuetracker.google.com/issues/370546370
doNotNagAbout(
		"Declaring an 'is-' property with a Boolean type has been deprecated. " +
				"Starting with Gradle 9.0, this property will be ignored by Gradle. " +
				"The combination of method name and return type is not consistent with Java Bean property rules and will become unsupported in future versions of Groovy. " +
				"Add a method named 'getCrunchPngs' with the same behavior and mark the old one with @Deprecated, " +
				"or change the type of 'com.android.build.gradle.internal.dsl.BuildType\$AgpDecorated.isCrunchPngs' (and the setter) to 'boolean'. " +
				"Consult the upgrading guide for further information: " +
				"https://docs.gradle.org/${gradleVersion}/userguide/upgrading_version_8.html#groovy_boolean_properties"
)
doNotNagAbout(
		"Declaring an 'is-' property with a Boolean type has been deprecated. " +
				"Starting with Gradle 9.0, this property will be ignored by Gradle. " +
				"The combination of method name and return type is not consistent with Java Bean property rules and will become unsupported in future versions of Groovy. " +
				"Add a method named 'getUseProguard' with the same behavior and mark the old one with @Deprecated, " +
				"or change the type of 'com.android.build.gradle.internal.dsl.BuildType.isUseProguard' (and the setter) to 'boolean'. " +
				"Consult the upgrading guide for further information: " +
				"https://docs.gradle.org/${gradleVersion}/userguide/upgrading_version_8.html#groovy_boolean_properties"
)
doNotNagAbout(
		"Declaring an 'is-' property with a Boolean type has been deprecated. " +
				"Starting with Gradle 9.0, this property will be ignored by Gradle. " +
				"The combination of method name and return type is not consistent with Java Bean property rules and will become unsupported in future versions of Groovy. " +
				"Add a method named 'getWearAppUnbundled' with the same behavior and mark the old one with @Deprecated, " +
				"or change the type of 'com.android.build.api.variant.impl.ApplicationVariantImpl.isWearAppUnbundled' (and the setter) to 'boolean'. " +
				"Consult the upgrading guide for further information: " +
				"https://docs.gradle.org/${gradleVersion}/userguide/upgrading_version_8.html#groovy_boolean_properties"
)

// TODEL Gradle 8.14 vs AGP 8.9 https://issuetracker.google.com/issues/408334529
doNotNagAbout(
		"Retrieving attribute with a null key. " +
				"This behavior has been deprecated. " +
				"This will fail with an error in Gradle 10.0. " +
				"Don't request attributes from attribute containers using null keys. " +
				"Consult the upgrading guide for further information: " +
				"https://docs.gradle.org/${gradleVersion}/userguide/upgrading_version_8.html#null-attribute-lookup",
		"at com.android.build.gradle.internal.ide.dependencies.ArtifactUtils.isAndroidProjectDependency(ArtifactUtils.kt:539)",
)
